ravikiran.web3

medium

# Incorrect configuration of Multi strategy can result in loss of funds.

## Summary
MultiStrategy is configured and managed by assigned manager using the setStrategy function. The setStrategy function is design to validate the strategy by accounting for weight of each element to sum to 100. Incase the total weight does not match 100, the function will revert.

But. there is a potential for error in setup, where elements are setup to bypass the check leading to incorrect computations of multi position and their by impacting other functions.

The error is not out rightly noticeable as this is a silent error in computation. Participates who interacted with Wagmi while strategy was incorrectly setup could loss funds. 

This is a scenario of human error, but impact is far reaching and can be easily prevented with a check as suggested in the recommendation as a defensive approach.

## Vulnerability Detail
**MultiStrategy: setStrategy function()**
The setStrategy function checks for total weight by summing up the weight of each strategy element in the array, but does not check for any element with 0 weight. As each of the element  looks for the underlying pool using the poolFeeAmt, the strategy with 0 weight is also mapped back to an underlying pool.

**Multipool:_initializeStrategy**
When functions _initializeStrategy() is called in Deposit or rebalanceAll, the _initializeStrategy() loops through all the elements of the strategy array and tries to process it by looking in the underlying pool. As such all elements in the strategy are accounted for computing the positions and updating the **multiPosition** state variable and their by impacting most of the calculations done in the Multi pool contract.

Below is the Proof of Concept to demonstrate the potential issue.
**POC:**

In the below test case, we are trying to setup an incorrect strategy for 3000 fee, with two entries with only one difference and that is the weight for strategy3000Right is 0.

1. const strategy3000
2. **const strategy3000Right**

```javascript
  it("should set a strategy successfully for manager only", async () => {
        const strategy500: IMultiStrategy.StrategyStruct = {
            tickSpacingOffset: 0,
            positionRange: 50,
            poolFeeAmt: 500,
            weight: 5000,
        }; // tickSpacing = 10
        const strategy3000: IMultiStrategy.StrategyStruct = {
            tickSpacingOffset: 0,
            positionRange: 420,
            poolFeeAmt: 3000,
            weight: 3000,
        }; // tickSpacing = 60
        const strategy3000Right: IMultiStrategy.StrategyStruct = {
            tickSpacingOffset: 0,
            positionRange: 420,
            poolFeeAmt: 3000,
            weight: 0,
        }; // tickSpacing = 60
        const strategy10000_left: IMultiStrategy.StrategyStruct = {
            tickSpacingOffset: -200,
            positionRange: 800,
            poolFeeAmt: 10000,
            weight: 1000,
        }; // tickSpacing = 200
        const strategy10000_right: IMultiStrategy.StrategyStruct = {
            tickSpacingOffset: 200,
            positionRange: 800,
            poolFeeAmt: 10000,
            weight: 1000,
        }; // tickSpacing = 200
        const strategy = [strategy500, strategy3000, **strategy3000Right**, strategy10000_left, strategy10000_right];
       
        await multistrategy.connect(manager).setStrategy(strategy);
```

Since the above incorrect setup will not be caught by setStrategy function, the strategy will be successfully configured and when _initializeStrategy function is called via rebalanceAll function,  the multiPosition state variable in Multipool will be updated. This will result in incorrect calculations in functions referring MultiPosition.

**Results before making the change in strategy in Concentrator.ts test script:**

usdtAmountToDeposit:made deposit BigNumber { value: "999999999" }
wethAmountToDeposit:made deposit BigNumber { value: "555588736808788669" }
    ✓ should make a deposit successfully (1387ms)
emits event:bob BigNumber { value: "**555588737642171775**" } BigNumber { value: "999999999" }
    ✓ emits event Deposit (818ms)
Bob Shares BigNumber { value: "**23570929945072**" }
    ✓ dispatcher:should make a deposit successfully (377ms)
Bob Claimed BigNumber { value: "**23570921498945**" }

**Results after making the change in strategy in Concentrator.ts test script:**
  ✓ a firs deposit fail if caller is not manager (41ms)
usdtAmountToDeposit:made deposit BigNumber { value: "999999999" }
wethAmountToDeposit:made deposit BigNumber { value: "602509563176856040" }
    ✓ should make a deposit successfully (1612ms)
emits event:bob BigNumber { value: "**623585325282503871**" } BigNumber { value: "999999999" }
    ✓ emits event Deposit (911ms)
Bob Shares BigNumber { value: "**17907259997968**" }
    ✓ dispatcher:should make a deposit successfully (490ms)
Bob Claimed BigNumber { value: "**17907254704114**" }
    ✓ dispatcher:should make a claim successfully (2296ms)


## Impact
Loss of funds and some of the functionality of the protocol not working as expected.

## Code Snippet

https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/MultiStrategy.sol#L57-L91

In the below function, an incorrect strategy can be passed as long as total weight tallies with 100. One or more elements of the parameter array can have weight set 0 to satisfy the checking criteria.

```solidity
 function setStrategy(Strategy[] calldata _currentStrategy) external onlyOwner {
        delete currentStrategy;
        uint256 weightSum;
        uint24 checkSortedFee;
        for (uint256 i = 0; i < _currentStrategy.length; ) {
            Strategy memory sPosition = _currentStrategy[i];
            ErrLib.requirement(
                checkSortedFee <= sPosition.poolFeeAmt,
                ErrLib.ErrorCode.SHOULD_BE_SORTED_BY_FEE
            );
            checkSortedFee = sPosition.poolFeeAmt;
            IMultipool.UnderlyingPool memory uPool = multipool.underlyingTrustedPools(
                sPosition.poolFeeAmt
            );
            if (uPool.poolAddress == address(0)) {
                revert InvalidFee(sPosition.poolFeeAmt);
            }
            _checkpositionsRange(
                sPosition.positionRange,
                uPool.tickSpacing,
                sPosition.tickSpacingOffset
            );
            ErrLib.requirement(
                sPosition.tickSpacingOffset % uPool.tickSpacing == 0,
                ErrLib.ErrorCode.INVALID_TICK_SPACING
            );
            weightSum += sPosition.weight;
            currentStrategy.push(_currentStrategy[i]);
            unchecked {
                ++i;
            }
        }
        ErrLib.requirement(weightSum == MAX_WEIGHT_UINT256, ErrLib.ErrorCode.INVALID_WEIGHTS_SUM);
        emit SetNewStrategy(_currentStrategy);
    }
```


https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L272-L311

This function is called in rebalanceAll() which will take the strategy configuration and apply to compute positions. This function loops through all the elements of the strategy array and update the multiPosition using the erroneous  strategy setup.

```solidity
function _initializeStrategy() private returns (Slot0Data[] memory) {
        uint256 positionsNum = strategy.strategySize();
        ErrLib.requirement(positionsNum > 0, ErrLib.ErrorCode.STRATEGY_DOES_NOT_EXIST);
        delete multiPosition;
        PositionInfo memory position;
        int24 upperTick;
        int24 lowerTick;
        Slot0Data[] memory slots = new Slot0Data[](positionsNum);

        for (uint256 i = 0; i < positionsNum; ) {
            IMultiStrategy.Strategy memory sPosition = strategy.getStrategyAt(i);
            UnderlyingPool memory uPool = underlyingTrustedPools[sPosition.poolFeeAmt];
            position.poolFeeAmt = sPosition.poolFeeAmt;
            position.weight = sPosition.weight;
            position.poolAddress = uPool.poolAddress;
            (slots[i].currentSqrtRatioX96, slots[i].tick, , , , , ) = IUniswapV3Pool(
                uPool.poolAddress
            ).slot0();
            (lowerTick, upperTick) = _getTicksForPosition(
                slots[i].tick,
                sPosition.positionRange,
                sPosition.tickSpacingOffset,
                uPool.tickSpacing
            );
            _checkTicks(lowerTick, upperTick, uPool.tickSpacing);
            position.upperTick = upperTick;
            position.lowerTick = lowerTick;
            position.positionKey = PositionKey.compute(
                address(this),
                position.lowerTick,
                position.upperTick
            );

            multiPosition.push(position);
            unchecked {
                ++i;
            }
        }
        return slots;
    }
```

## Tool used

Manual Review  and Testing scripts

## Recommendation
In the setStrategy() function,  add an additional check for weight for each of the strategy element to be not equal to 0
in additional to checking it to be 100. This will prevent such incorrect setup in setStrategy() function itself.


```solidity
 function setStrategy(Strategy[] calldata _currentStrategy) external onlyOwner {
        delete currentStrategy;
        uint256 weightSum;
        uint24 checkSortedFee;
        for (uint256 i = 0; i < _currentStrategy.length; ) {
            Strategy memory sPosition = _currentStrategy[i];
            require(sPosition.weight > 0,"Invalid weight");
            ErrLib.requirement(
                checkSortedFee <= sPosition.poolFeeAmt,
                ErrLib.ErrorCode.SHOULD_BE_SORTED_BY_FEE
            );
            checkSortedFee = sPosition.poolFeeAmt;
            IMultipool.UnderlyingPool memory uPool = multipool.underlyingTrustedPools(
                sPosition.poolFeeAmt
            );
            if (uPool.poolAddress == address(0)) {
                revert InvalidFee(sPosition.poolFeeAmt);
            }
            _checkpositionsRange(
                sPosition.positionRange,
                uPool.tickSpacing,
                sPosition.tickSpacingOffset
            );
            ErrLib.requirement(
                sPosition.tickSpacingOffset % uPool.tickSpacing == 0,
                ErrLib.ErrorCode.INVALID_TICK_SPACING
            );
            weightSum += sPosition.weight;
            currentStrategy.push(_currentStrategy[i]);
            unchecked {
                ++i;
            }
        }
        ErrLib.requirement(weightSum == MAX_WEIGHT_UINT256, ErrLib.ErrorCode.INVALID_WEIGHTS_SUM);
        emit SetNewStrategy(_currentStrategy);
    }
```
