lil.eth

medium

# Unchecked slots may lead to imbalance in token ratios within the contract

## Summary
 In Multipool.sol#deposit() function, if no slots are defined, it may allow for tokens to be added to the contract without minting liquidity on Uniswap V3 pools. This scenario can disrupt the system by possibly imbalancing the token ratio within the contract.

## Vulnerability Detail

The "deposit" function is designed to handle deposits of token pairs into the contract. It checks if the contract's total supply is zero, and if so, it will initialize the strategy and mint a liquidity token to the sender.

However, in the else condition, where the total supply isn't zero, the function fetches the slots with "getSlots()" but does not validate if any slots exist. **If no slots are defined, it proceeds to execute the deposit function without minting liquidity in the Uniswap V3 pool.** This may allow users to deposit tokens into the contract without creating a corresponding liquidity position in Uniswap V3 pools. This scenario can imbalance the token ratio within the contract and potentially manipulate the system.

## Impact

If an attacker can repeatedly perform this action, it may disrupt the contract's internal balance, leading to improper distribution of tokens when liquidity is withdrawn. This could cause financial loss to users or potentially make the contract unusable.

## Code Snippet
https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L253
```solidity
    function deposit(
        // TODO:  PAUSED + Max CAPS
        uint256 amount0Desired,
        uint256 amount1Desired,
        uint256 amount0Min,
        uint256 amount1Min
    ) external returns (uint256 lpAmount) {
        ErrLib.requirement(
            amount0Desired > MINIMUM_AMOUNT && amount1Desired > MINIMUM_AMOUNT,
            ErrLib.ErrorCode.AMOUNT_TOO_SMALL
        );
        uint256 _totalSupply = totalSupply();
        Slot0Data[] memory slots;

        if (_totalSupply == 0) {
            ErrLib.requirement(
                msg.sender == owner(),
                ErrLib.ErrorCode.FIRST_DEPOSIT_SHOULD_BE_MAKE_BY_OWNER
            );
            // fetched from Uniswap codebase
            lpAmount = Babylonian.sqrt(amount0Desired * amount1Desired) - MINIMUM_LIQUIDITY;
            _mint(burnAddress, MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
            slots = _initializeStrategy();
        } else {
            slots = getSlots(); //E @audit-issue no check if slots exists
            (uint256 reserve0, uint256 reserve1, , ) = _getReserves(slots);
            (amount0Desired, amount1Desired) = _optimizeAmounts(
                amount0Desired,
                amount1Desired,
                amount0Min,
                amount1Min,
                reserve0,
                reserve1
            );
            // MINIMUM
            uint256 l0 = (amount0Desired * _totalSupply) / reserve0;
            uint256 l1 = (amount1Desired * _totalSupply) / reserve1;
            lpAmount = l0 < l1 ? l0 : l1;
        }

        ErrLib.requirement(lpAmount > 0, ErrLib.ErrorCode.INSUFFICIENT_LIQUIDITY_MINTED);
        ErrLib.requirement(
            maxTotalSupply >= _totalSupply + lpAmount,
            ErrLib.ErrorCode.MAX_TOTAL_SUPPLY_REACHED
        );
        _pay(token0, msg.sender, address(this), amount0Desired);
        _pay(token1, msg.sender, address(this), amount1Desired);

        _deposit(amount0Desired, amount1Desired, _totalSupply, slots);

        _mint(msg.sender, lpAmount);

        emit Deposit(msg.sender, amount0Desired, amount1Desired, lpAmount);
    }
```
## Tool used

Manual Review

## Recommendation
To avoid this potential issue, a validation check should be added after the "getSlots()" function is called, to ensure that the returned slots array is not empty. If it is empty, the function should revert, thus avoiding any disruption to the token ratio within the contract. It's crucial to maintain the correspondence between tokens in the contract and liquidity in the Uniswap V3 pools.

Here's a suggested change:
```solidity
slots = getSlots();
ErrLib.requirement(
    slots.length > 0,
    ErrLib.ErrorCode.NO_DEFINED_SLOTS
);

```