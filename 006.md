tsvetanovv

high

# Loss of fees if the user withdraws all shares

## Summary

Loss of fees in `withdraw()` if the user decides to withdraw all shares because fees go to users only in `else if` logic.

## Vulnerability Detail

In `Dispatcher.sol` we have `withdraw()` function:
```soldiity
function withdraw(uint256 pid, uint256 amount, uint256 deviationBP) external checkPid(pid) {
        PoolInfo memory pool = poolInfo[pid];
        UserInfo storage user = userInfo[pid][msg.sender];
        (
            uint256 reserve0,
            uint256 reserve1,
            IMultipool.FeeGrowth memory feesGrow,
            uint256 _totalSupply
        ) = IMultipool(pool.multipool).snapshot();

        require(user.shares > 0, "user.shares is 0");
        require(_totalSupply > 0, "sharesTotal is 0");
        if (amount > user.shares) {
            // withdraw witout claiming
            amount = user.shares;
        } else if (amount < user.shares) {  
            uint256 lpAmount;
            {
                (uint256 fee0, uint256 fee1) = _calcFees(feesGrow, user);
                lpAmount = _estimateWithdrawalLp(reserve0, reserve1, _totalSupply, fee0, fee1);
            }
            user.shares -= lpAmount;
            _withdrawFee(pool, lpAmount, reserve0, reserve1, _totalSupply, deviationBP);
        }

        uint256 sharesRemoved = amount > user.shares ? user.shares : amount;
        user.shares -= sharesRemoved;  

        user.feeDebt0 = feesGrow.accPerShare0;
        user.feeDebt1 = feesGrow.accPerShare1;
        _pay(pool.multipool, address(this), msg.sender, sharesRemoved);

        emit Withdraw(msg.sender, pid, amount);
    }
```
This function allows a user to withdraw their Lp-share from a specific pool and receive their proportionate share of fees.

Special attention should be paid to the following code:
```solidity
        if (amount > user.shares) {
            // withdraw witout claiming
            amount = user.shares;
        } else if (amount < user.shares) {  

            uint256 lpAmount;
            {
                (uint256 fee0, uint256 fee1) = _calcFees(feesGrow, user);
                lpAmount = _estimateWithdrawalLp(reserve0, reserve1, _totalSupply, fee0, fee1);
            }
            user.shares -= lpAmount;
            _withdrawFee(pool, lpAmount, reserve0, reserve1, _totalSupply, deviationBP);
        }
```

We can see that if a user makes a request to withdraw more shares than he have or an equal amount, we fail to enter the `else if` statement. And the problem is that the logic for receiving fees is only there. 
Even after that he can't call the function again because there will be no more shares and he will be stopped on:

```solidity
225: require(user.shares > 0, "user.shares is 0");
```
## Impact

Loss of fees 

## Code Snippet

https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Dispatcher.sol#L215-L248

## Tool used

Manual Review

## Recommendation

The logic for receiving fees should be taken out of `else if` statement.