Juntao

high

# LP tokens may not be fairly minted due to the different Protocol Fee Rate between Multipool and UniswapV3Pool

## Summary

If Multipool has a lower `Protocol Fee Rate` than UniswapV3Pool, later depositers may get more LP tokens than eariler depositers.

## Vulnerability Detail

In [Multipool](https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L18) contract, function [deposit(...)](https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L433-L486) is called by users to deposit funds and mint LP tokens. If the [_totalSupply](https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L444) of the pool's LP token is not 0, protocol will calculate pool `reserves` and LP token amount to be minted for user:
```solidity
} else {
    slots = getSlots();
    (uint256 reserve0, uint256 reserve1, , ) = _getReserves(slots);
    (amount0Desired, amount1Desired) = _optimizeAmounts(
        amount0Desired,
        amount1Desired,
        amount0Min,
        amount1Min,
        reserve0,
        reserve1
    );
    // MINIMUM
    uint256 l0 = (amount0Desired * _totalSupply) / reserve0;
    uint256 l1 = (amount1Desired * _totalSupply) / reserve1;
    lpAmount = l0 < l1 ? l0 : l1;
}
```
The way of how `reserves` is calculated is defined in function [_getReserves(...)](https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L693C14-L746), and `reserves` is composed of 3 parts:

- token0 and token1 balance in the pool contract
- token0 and token1 balance (provided liquidity) owned by the UniswapV3Pool position
- uncollected (accumulated and unaccumulated) position fees in the underlying Uniswap pool

Let's focus on how uncollected position fees are counted in the calculation:
 1. protocol retrieves `liquidity` and accumulated fees (`tokensOwed0`, `tokensOwed1`) from Uniswap Pool and adds them to `reserves`:
```solidity
(
    liquidity,
    feeGrowthInside0LastX128,
    feeGrowthInside1LastX128,
    tokensOwed0,
    tokensOwed1
) = IUniswapV3Pool(position.poolAddress).positions(position.positionKey);
reserve0 += tokensOwed0;
reserve1 += tokensOwed1;
```
2. If `liquidity` is not 0, protocol calculates unaccumulated fees (fees earned in swaps but not updated to accumulated fees yet) and adds them to `reserves`:
```solidity
if (liquidity > 0) {
    (
        uint256 feeGrowthInside0X128Pending,
        uint256 feeGrowthInside1X128Pending
    ) = _getFeeGrowthInside(
            IUniswapV3Pool(position.poolAddress),
            slots[i].tick,
            position.lowerTick,
            position.upperTick
        );
    pendingFee0 += uint128(
        FullMath.mulDiv(
            feeGrowthInside0X128Pending - feeGrowthInside0LastX128,
            liquidity,
            FixedPoint128.Q128
        )
    );
    pendingFee1 += uint128(
        FullMath.mulDiv(
            feeGrowthInside1X128Pending - feeGrowthInside1LastX128,
            liquidity,
            FixedPoint128.Q128
        )
    );
}

...

uint256 feeGrowthWeight = MAX_WEIGHT_UINT256 - protocolFeeWeight;
pendingFee0 = (pendingFee0 * feeGrowthWeight) / MAX_WEIGHT_UINT256;
pendingFee1 = (pendingFee1 * feeGrowthWeight) / MAX_WEIGHT_UINT256;


reserve0 += pendingFee0;
reserve1 += pendingFee1;
```
Please note the unaccumulated fees are decuted by Multipool Protocol Fee, which's rate is defined as [protocolFeeWeight](https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L82) in Multipool contract:
```solidity
    uint256 public protocolFeeWeight = 2000; // 20%
```
This may be problematic because if the `Protocol Fee Rate` in the underlying UniswapV3Pool (initialized to
zero, but can changed by UNI governance) is higher than that is in the Multipool, LP tokens may not be fairly minted.

Let's assume the `Protocol Fee Rate` in UniswapV3Pool is 25% (permitted values are 0, 1/4, 1/5, 1/6, 1/7, 1/8, 1/9, or 1/10), and at some point of time, the pool status is as below (for the sake of simplicity, we use 'token' to represent both token0 and token1):

> Token Balance: 1000
> Accumulated Fees: 0
> Unaccumulated Fees: 200

UserA deposits 10 tokens, `reservesA` is `1000 + 0 + 200 * (1 - 20%) = 1160`.
After deposit, the total supply of LP token is updated to `totalSupplyA` and pool status is as below:

> Token Balance: 1000 + 10 = 1010
> Accumulated Fees: 200 * (1 - 25%) = 150
> Unaccumulated Fees: 0

UserB also deposits 10 tokens, `reservesB` is `1010 + 150 + 0 * (1 - 20%) = 1160`.
After deposit, `totalSupplyA` is updated to `totalSupplyB`

During each deposit, LP tokens are minted for each user:
```solidity
// MINIMUM
uint256 l0 = (amount0Desired * _totalSupply) / reserve0;
uint256 l1 = (amount1Desired * _totalSupply) / reserve1;
lpAmount = l0 < l1 ? l0 : l1;
```
As `reservesA` is equal to `reservesB` and `totalSupplyB` is larger than `totalSupplyA`, it is easy to tell that UserB will get more LP tokens than UserA.

## Impact

- Later depositers can get more earnings with more LP tokens, which is obviously unfair to earlier depositers.
- Later depositers can withdraw more funds with more LP tokens, which means a loss of funds to earlier depositers.

## Code Snippet

https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Multipool.sol#L693-L768

## Tool used

Manual Review

## Recommendation

Please consider to add an offset (non-negative) to UniswapV3Pool's protocol fee rate as `protocolFeeWeight` in Multipool,  to ensure Mutipool's `Protocol Fee Rate` is always greater than or equal to UniswapV3Pool's.