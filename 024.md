mahdiRostami

medium

# Reentrancy in deposit and withdraw in Dispatcher

## Summary
The best practice in external calling is changing state then calling.
## Vulnerability Detail
In the “deposit”  and “withdraw” functions:
```solidity
     function deposit(uint256 pid, uint256 amount, uint256 deviationBP) external checkPid(pid) {
        PoolInfo memory pool = poolInfo[pid];
        UserInfo storage user = userInfo[pid][msg.sender];
        (
            uint256 reserve0,
            uint256 reserve1,
            IMultipool.FeeGrowth memory feesGrow,
            uint256 _totalSupply
        ) = IMultipool(pool.multipool).snapshot();
        if (user.shares > 0) {
            uint256 lpAmount;
            {
                (uint256 fee0, uint256 fee1) = _calcFees(feesGrow, user);
                lpAmount = _estimateWithdrawalLp(reserve0, reserve1, _totalSupply, fee0, fee1);
            }
            user.shares -= lpAmount;
            _withdrawFee(pool, lpAmount, reserve0, reserve1, _totalSupply, deviationBP);
```
if the user has shares, the contract call "_calcFees" in "_calcFees" 
```solidity
    function _calcFees(
        IMultipool.FeeGrowth memory growth,
        UserInfo memory user
    ) private pure returns (uint256 amount0, uint256 amount1) {
        amount0 = FullMath.mulDiv(
            user.shares,
            growth.accPerShare0 - user.feeDebt0,
            FixedPoint128.Q128
        );
        amount1 = FullMath.mulDiv(
            user.shares,
            growth.accPerShare1 - user.feeDebt1,
            FixedPoint128.Q128
        );
    }
```
the contract uses "user.feeDebt0" for calculating and then "deposit" function call "_withdrawFee" for paying fees,
```solidity
    function _withdrawFee(
        PoolInfo memory pool,
        uint256 lpAmount,
        uint256 reserve0,
        uint256 reserve1,
        uint256 _totalSupply,
        uint256 deviationBP
    ) private {
        uint256 amount0OutMin = (reserve0 * lpAmount * deviationBP) /
            (_totalSupply * MAX_DEVIATION);
        uint256 amount1OutMin = (reserve1 * lpAmount * deviationBP) /
            (_totalSupply * MAX_DEVIATION);
        (uint256 withdrawnAmount0, uint256 withdrawnAmount1) = IMultipool(pool.multipool).withdraw(
            lpAmount,
            amount0OutMin,
            amount1OutMin
        );
        _pay(pool.token0, address(this), msg.sender, withdrawnAmount0);
        _pay(pool.token1, address(this), msg.sender, withdrawnAmount1);
```
```solidity
    function _pay(address token, address payer, address recipient, uint256 value) private {
        if (value > 0) {
            if (payer == address(this)) {
                IERC20(token).safeTransfer(recipient, value);
            } else {
                IERC20(token).safeTransferFrom(payer, recipient, value);
            }
        }
    }
```
 but "deposit" function updates "user.feeDebt0" after calling "_withdrawFee".

An attacker can call withdraw or deposit first and, in the fallback call, call the other one or the same function.
## Impact
wrong calculation in fees
## Code Snippet
affected code :
Deposit
[https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Dispatcher.sol#L180-L207](https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Dispatcher.sol#L180-L207)
withdraw
[https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Dispatcher.sol#L215-L248](https://github.com/sherlock-audit/2023-06-real-wagmi/blob/main/concentrator/contracts/Dispatcher.sol#L215-L248)
## Tool used
Manual Review
## Recommendation
Update 
```solidity
        user.feeDebt0 = feesGrow.accPerShare0;
        user.feeDebt1 = feesGrow.accPerShare1;
```
before 
```solidity
            _withdrawFee(pool, lpAmount, reserve0, reserve1, _totalSupply, deviationBP);
```

mitigation:
```git
             user.shares -= lpAmount;
+            user.feeDebt0 = feesGrow.accPerShare0; 
+            user.feeDebt1 = feesGrow.accPerShare1;
             _withdrawFee(pool, lpAmount, reserve0, reserve1, _totalSupply, deviationBP); 
         }
        if (amount > 0) { 
             _pay(pool.multipool, msg.sender, address(this), amount);
             user.shares += amount;
         }
-        user.feeDebt0 = feesGrow.accPerShare0;
-        user.feeDebt1 = feesGrow.accPerShare1;
```